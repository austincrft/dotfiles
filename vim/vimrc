" Set local foldmarker
" vim:fdm=marker

" Editor {{{

" Use Vim settings, rather then Vi settings
" This must be first, because it changes other options as a side effect
set nocompatible

" ------------------------------------------------------------------------------
" General Config
" ------------------------------------------------------------------------------
set number                     " Line numbers
set relativenumber             " Relative numbering
set backspace=indent,eol,start " Allow backspace in insert mode
set history=1000               " Store lots of :cmdline history
set showcmd                    " Show incomplete cmds down the bottom
set showmode                   " Show current mode down the bottom
set visualbell                 " No sounds
set autoread                   " Reload files changed outside vim
set hidden                     " Allow buffers to exist in background
set noswapfile                 " No swap file
set nobackup                   " No backup
set nowb                       " No write-backup
set laststatus=2               " Always show statusline
set t_Co=16                    " Set terminal color to 16
set t_md=                      " Turn off bold fonts
set colorcolumn=80             " Highlight column 80
set textwidth=80               " Textwidth at column 80
set formatoptions-=t           " Don't auto-wrap at textwidth
set formatoptions-=o           " Don't auto-insert comment character
set cursorline                 " Highlight current line
set completeopt=menuone        " Complete option (don't show preview)
set wildmenu                   " Tab through commands
set cryptmethod=blowfish2      " Better encryption
set mouse=a                    " Mouse support
set diffopt+=vertical          " Prefer vertical diff
set exrc                       " Allow project-specific vimrc
set secure                     " Force safety in project-specific vimrc
syntax on                      " Turn on syntax
syntax enable                  " Enable syntax
filetype plugin indent on      " Enable filetypes

" ------------------------------------------------------------------------------
" Default for tabs
" ------------------------------------------------------------------------------
set tabstop=4
set softtabstop=4
set shiftwidth=4

" ------------------------------------------------------------------------------
" Indentation and Linebreaks
" ------------------------------------------------------------------------------
set autoindent
set smartindent
set breakindent
set smarttab
set expandtab
set nowrap
set linebreak
set listchars=eol:¬,tab:→-,trail:·,extends:>,precedes:<

" ------------------------------------------------------------------------------
" Search Config
" ------------------------------------------------------------------------------
set incsearch
set ignorecase
set smartcase

" ------------------------------------------------------------------------------
"  Set ESC and Leader
" ------------------------------------------------------------------------------
imap jj <Esc>
let mapleader=' '

" ------------------------------------------------------------------------------
" Normal Mappings
" ------------------------------------------------------------------------------
nnoremap gb :ls<CR>:buffer<Space>
if (!has('win32'))
    nnoremap <C-P> :FZF<CR>
endif

" ------------------------------------------------------------------------------
" Visual Mappings
" ------------------------------------------------------------------------------
vnoremap * "zy/<C-R>z<CR>| " Search for selection using z-register
vnoremap @@ :norm! @@<CR>| " Replay last macro on selection

" ------------------------------------------------------------------------------
" Command Mappings
" ------------------------------------------------------------------------------
cmap w!! w !sudo tee > /dev/null %| " Save as sudo

" ------------------------------------------------------------------------------
" Fold Config
" ------------------------------------------------------------------------------
set foldmethod=indent
" Set foldlevel to 1 > max fold
autocmd BufWinEnter *
    \ let &foldlevel = max(map(range(1, line('$')), 'foldlevel(v:val)'))

" ------------------------------------------------------------------------------
"  Configure Directories
" ------------------------------------------------------------------------------
set runtimepath=$HOME/.vim,$HOME/.vim/after,$VIMRUNTIME

" ------------------------------------------------------------------------------
"  Configure Environment
" ------------------------------------------------------------------------------
if !has('win32')
    let $BASH_ENV = "~/.bash_aliases"
endif

" ------------------------------------------------------------------------------
" Persistent Undo -- Keep undo history across sessions by storing in file
" ------------------------------------------------------------------------------
if has('persistent_undo')
    silent !mkdir ~/.vim/backups > /dev/null 2>&1
    set undodir=~/.vim/backups
    set undofile
endif

" ------------------------------------------------------------------------------
" GVIM
" ------------------------------------------------------------------------------
if has("gui_running")
    set guifont=ProggyCleanTT:h12
    set lines=45
    set columns=125
    set encoding=utf-8

    set guioptions-=m
    set guioptions-=T
    set guioptions-=r
    set guioptions-=L
endif

" }}}

" Functions {{{

" ------------------------------------------------------------------------------
" Get C# Namespace
" ------------------------------------------------------------------------------
function! GetCSharpNamespace(filename, levels)
    let l:directory = fnamemodify(a:filename, ':h') . '/'

    let l:c = 0
    let l:traversed = []

    while l:c <= a:levels
        let l:dir_name = fnamemodify(resolve(fnamemodify(l:directory, ':h')), ':t')
        let l:traversed = l:traversed + [l:dir_name]

        let l:matches = globpath(l:directory, '*.csproj', 0, 1)
        if !empty(l:matches)
            break
        endif

        let l:directory = l:directory . '../'
        let l:c += 1
    endwhile

    return join(reverse(traversed), '.')
endfunction

" ------------------------------------------------------------------------------
" Escape XML
" ------------------------------------------------------------------------------
function! EscapeXml(unescape)
    if a:unescape == 0
        silent! s/&/\&amp;/g
        silent! s/"/\&quot;/g
        silent! s/'/\&apos;/g
        silent! s/'/\&#39;/g
        silent! s/’/\&#8217;/g
        silent! s/>/\&gt;/g
        silent! s/</\&lt;/g
    else
        silent! s/&quot;/"/g
        silent! s/&apos;/'/g
        silent! s/&#39;/'/g
        silent! s/&#8217;/’/g
        silent! s/&gt;/>/g
        silent! s/&lt;/</g
        silent! s/&amp;/\&/g
    endif
endfunction

" ------------------------------------------------------------------------------
" Toggle background
" ------------------------------------------------------------------------------
function! ToggleBackground()
    if &background ==# 'dark'
        set background=light
    else
        set background=dark
    endif
endfunction

" ------------------------------------------------------------------------------
" Toggle wrap
" ------------------------------------------------------------------------------
function! ToggleWrap()
  if &wrap ==# 'nowrap'
    setlocal wrap
  else
    setlocal nowrap
  endif
endfunction

" ------------------------------------------------------------------------------
" Read Command
" ------------------------------------------------------------------------------
function! ReadCommand(msgcmd)
    " Capture message into variable
    redir => message
    silent execute a:msgcmd
    redir END

    " Paste to buffer
    silent put=message
endfunction
command! -nargs=+ -complete=command ReadCommand call ReadCommand(<q-args>)

" ------------------------------------------------------------------------------
" InsertDate Command
" ------------------------------------------------------------------------------
function! InsertDate()
    execute "put =strftime('%Y-%m-%d')"
    execute 'norm! kJ'
endfunction
command! -nargs=0 -complete=command InsertDate call InsertDate()

" ------------------------------------------------------------------------------
" RunCtags Command
" ------------------------------------------------------------------------------
function! RunCtags()
    execute 'AsyncRun ctags -R'
    execute 'botright copen'
endfunction

" ------------------------------------------------------------------------------
" Ag Command
" ------------------------------------------------------------------------------
function! Ag(text, ...)
    let a:filetype = get(a:, 1, '')
    execute 'AsyncRunWithQf ag --vimgrep ' . a:filetype . ' "' . a:text . '"'
endfunction
command! -nargs=* -complete=command Ag call Ag(<f-args>)

" ------------------------------------------------------------------------------
" AgG Command
" ------------------------------------------------------------------------------
function! AgG(text, filePattern)
    execute 'AsyncRun! ag --vimgrep -G ' . a:filePattern . ' "' . a:text . '"'
    execute 'botright copen'
endfunction
command! -nargs=* -complete=command AgG call AgG(<f-args>)

" ------------------------------------------------------------------------------
" Scratch Command
" ------------------------------------------------------------------------------
function! Scratch()
    execute 'top 15sp'
    execute 'enew'
    execute 'setlocal buftype=nofile'
    execute 'setlocal bufhidden=hide'
    execute 'setlocal noswapfile'
    execute 'file [Scratch]'
endfunction
command! -nargs=0 -complete=command Scratch call Scratch()

" ------------------------------------------------------------------------------
" DeleteInactiveBufs Command
" source: https://stackoverflow.com/a/7321131/3661319
" ------------------------------------------------------------------------------
function! DeleteInactiveBufs()
    " From tabpagebuflist() help, get a list of all buffers in all tabs
    let tablist = []
    for i in range(tabpagenr('$'))
        call extend(tablist, tabpagebuflist(i + 1))
    endfor

    " Below originally inspired by Hara Krishna Dara and Keith Roberts
    " http://tech.groups.yahoo.com/group/vim/message/56425
    let nWipeouts = 0
    for i in range(1, bufnr('$'))
        if bufexists(i) && !getbufvar(i,"&mod") && index(tablist, i) == -1
        " bufno exists AND isn't modified AND isn't in the list of buffers open
        " in windows and tabs
            silent exec 'bwipeout' i
            let nWipeouts = nWipeouts + 1
        endif
    endfor
    echomsg nWipeouts . ' buffer(s) wiped out'
endfunction
command! -nargs=0 -complete=command DeleteInactiveBufs call DeleteInactiveBufs()

" ------------------------------------------------------------------------------
" AsyncRunWithQf Command
" ------------------------------------------------------------------------------
function! AsyncRunWithQf(cmd)
    execute 'AsyncRun! ' . a:cmd
    execute 'botright copen'
endfunction
command! -nargs=1 -complete=command AsyncRunWithQf call AsyncRunWithQf(<f-args>)

" ------------------------------------------------------------------------------
" CopyFilePath Command
" ------------------------------------------------------------------------------
function! CopyFilePath()
     let @+ = expand('%:p')
endfunction
command! -nargs=0 -complete=command CopyFilePath call CopyFilePath()

" }}}

" {{{ Misc Config

" ------------------------------------------------------------------------------
" ConEmu Config
" ------------------------------------------------------------------------------
if !empty($CONEMUBUILD)
    " Visual
    set term=xterm
    set encoding=utf-8
    set termencoding=&encoding
    set t_Co=256
    let &t_AB="\e[48;5;%dm"
    let &t_AF="\e[38;5;%dm"

    inoremap <Esc>[62~ <C-X><C-E>
    inoremap <Esc>[63~ <C-X><C-Y>
    nnoremap <Esc>[62~ <C-E>
    nnoremap <Esc>[63~ <C-Y>

    " Backspace
    inoremap <Char-0x07F> <BS>
    nnoremap <Char-0x07F> <BS>
    cnoremap <Char-0x07F> <BS>
endif

" }}}

" Plugins {{{

" ------------------------------------------------------------------------------
" Plug Config
" ------------------------------------------------------------------------------
" Automatically download Plug if vim is started without it
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')

" Editor features
Plug 'jiangmiao/auto-pairs'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'michaeljsmith/vim-indent-object'
Plug 'sjl/gundo.vim'
Plug 'sk1418/QFGrep'
Plug 'jpalardy/vim-slime'

" File browsing
Plug 'tpope/vim-vinegar'

" Terminal fuzzy-searching
if (!has('win32'))
    Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
    Plug 'junegunn/fzf.vim'
else
    Plug 'ctrlpvim/ctrlp.vim'
endif

" Auto-Complete
Plug 'garbas/vim-snipmate'
    \ | Plug 'marcweber/vim-addon-mw-utils'
    \ | Plug 'tomtom/tlib_vim'

" Formatting
Plug 'godlygeek/tabular'
Plug 'ntpeters/vim-better-whitespace'

" Git
Plug 'tpope/vim-fugitive'

" Languages {{{

" Clojure
Plug 'tpope/vim-fireplace', { 'for': ['clojure', 'markdown'] }

" CSharp
Plug 'OrangeT/vim-csharp', { 'for': ['cs', 'markdown'] }
Plug 'OmniSharp/omnisharp-vim', { 'for': 'cs' }
Plug 'juliosueiras/cakebuild.vim', { 'for': 'cake' }

" FSharp
Plug 'fsharp/vim-fsharp', { 'for': ['fsharp', 'markdown'], 'do': 'make' }

" Go
Plug 'fatih/vim-go', { 'for': ['go', 'markdown'] }

" HTML
Plug 'alvan/vim-closetag'

" JavaScript
Plug 'pangloss/vim-javascript', { 'for': 'javascript' }
Plug 'ternjs/tern_for_vim', { 'for': ['javascript', 'markdown'], 'do': 'npm install' }
Plug 'prettier/vim-prettier', { 'do': 'yarn install', 'for': 'javascript' }

" Markdown
Plug 'tpope/vim-markdown', { 'for': ['markdown', 'vimwiki'] }

" Powershell
Plug 'PProvost/vim-ps1', { 'for': ['ps1', 'markdown'] }

" Python
Plug 'davidhalter/jedi-vim', { 'for': 'python' }

" Scala
Plug 'derekwyatt/vim-scala', { 'for': ['scala', 'markdown'] }

" SQL
Plug 'vim-scripts/dbext.vim', { 'for': 'sql' }

" TypeScript
Plug 'leafgarland/typescript-vim', { 'for': ['typescript', 'markdown'] }
Plug 'Quramy/tsuquyomi', { 'for': 'typescript' }

" }}}

" Linting
Plug 'w0rp/ale'

" Tags
Plug 'ludovicchabant/vim-gutentags'
Plug 'majutsushi/tagbar', { 'on': 'TagbarOpenAutoClose' }

" Testing
Plug 'janko-m/vim-test'

" Tools
Plug 'junegunn/goyo.vim', { 'on': 'Goyo' }
Plug 'kshenoy/vim-signature'
Plug 'tpope/vim-dispatch'
Plug 'skywind3000/asyncrun.vim'
Plug 'vim-scripts/BufOnly.vim'
Plug 'vimwiki/vimwiki'

" Visuals
Plug 'altercation/vim-colors-solarized'
Plug 'bling/vim-airline'
Plug 'vim-airline/vim-airline-themes'

call plug#end()

" }}}

" Plugin Config {{{

" ------------------------------------------------------------------------------
" Airline Config
" ------------------------------------------------------------------------------
let g:airline#extensions#hunks#enabled = 0
let g:airline_symbols_ascii = 1

" ------------------------------------------------------------------------------
" Ale Config
" ------------------------------------------------------------------------------
let g:ale_lint_on_text_changed = 'never'
let g:ale_linters = {'cs': []}

" ------------------------------------------------------------------------------
" CloseTag Config
" ------------------------------------------------------------------------------
let g:closetag_filenames = '*.html,*.xml,*.config'

" ------------------------------------------------------------------------------
" Color Scheme
" ------------------------------------------------------------------------------
let g:solarized_bold = 0
let g:solarized_underline = 0
let g:solarized_italic = 0
set background=dark
colorscheme solarized

" ------------------------------------------------------------------------------
" Commentary Config
" ------------------------------------------------------------------------------
autocmd FileType cs setlocal commentstring=//\ %s
autocmd FileType dosbatch setlocal commentstring=::\ %s
autocmd FileType sql setlocal commentstring=--\ %s

" ------------------------------------------------------------------------------
" Completion Config
" ------------------------------------------------------------------------------
" autocmd FileType cs setlocal omnifunc=OmniSharp#Complete
autocmd FileType python setlocal omnifunc=jedi#completions
autocmd Filetype fsharp setlocal omnifunc=fsharpbinding#python#Complete
autocmd Filetype typescript setlocal omnifunc=tsuquyomi#complete

" ------------------------------------------------------------------------------
" CtrlP Config
" ------------------------------------------------------------------------------
let g:ctrlp_clear_cache_on_exit = 0
let g:ctrlp_cache_dir = $HOME . '/.cache/ctrlp'
let g:ctrlp_working_path_mode = 'a'

if executable('ag')
    let g:ctrlp_user_command = 'ag -l --nocolor -g "" "%s"'
endif

if has('win32')
    let g:ctrlp_prompt_mappings = {
        \ 'PrtBS()': ['<Char-0x07F>', '<c-h>']
    \ }
endif

" ------------------------------------------------------------------------------
" FSharp Config
" ------------------------------------------------------------------------------
let g:fsharp_xbuild_path = 'msbuild'
let g:fsharp_test_runner = 'nunit-console'
let g:fsharp_only_check_errors_on_write = 1
let g:fsharp_map_keys = 0
let g:fsharp_completion_helptext = 0

" ------------------------------------------------------------------------------
" FZF Config
" ------------------------------------------------------------------------------
let g:fzf_colors =
\ { 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

" ------------------------------------------------------------------------------
" File Extension Config
" ------------------------------------------------------------------------------
autocmd BufRead,BufNewFile,BufEnter *.js set filetype=javascript

" ------------------------------------------------------------------------------
" Goyo Config
" ------------------------------------------------------------------------------
let g:goyo_width=81

" ------------------------------------------------------------------------------
" Jedi Config
" ------------------------------------------------------------------------------
let g:jedi#auto_vim_configuration = 0
let g:jedi#popup_on_dot = 0
let g:jedi#auto_initialization = 0
let g:jedi#show_call_signatures = 0

" ------------------------------------------------------------------------------
" Netrw Config
" ------------------------------------------------------------------------------
let g:netrw_localrmdir = 'rm -r' " Ability to remove non-empty directories

" ------------------------------------------------------------------------------
" OmniSharp Config
" ------------------------------------------------------------------------------
let g:OmniSharp_server_type = 'v1'
let g:Omnisharp_start_server = 0

" ------------------------------------------------------------------------------
" Prettier Config
" ------------------------------------------------------------------------------
let g:prettier#autoformat = 1
let g:prettier#exec_cmd_async = 1

" ------------------------------------------------------------------------------
" Signature Config
" ------------------------------------------------------------------------------
let g:SignatureMarkTextHLDynamic = 1
let g:SignatureMarkerTextHLDynamic = 1

" ------------------------------------------------------------------------------
" Slime Config
" ------------------------------------------------------------------------------
let g:slime_target = 'tmux'

" ------------------------------------------------------------------------------
" Snipmate Config
" ------------------------------------------------------------------------------
" Prevents errors after sourcing .vimrc
let g:snipMate = get(g:, 'snipMate', {})
" Don't insert anything when a snippet isn't found
let g:snipMate['no_match_completion_feedkeys_chars'] = ''
let g:snipMate.snippet_version = 1

" ------------------------------------------------------------------------------
" Tagbar Config
" ------------------------------------------------------------------------------
let g:tagbar_vertical = 30
let g:tagbar_map_showproto = 'o'
let g:tagbar_type_typescript = {
  \ 'kinds': [
    \ 'n:namespace:0:1',
    \ 'e:enums:0:1',
    \ 'M:module:0:1',
    \ 'I:import:0:1',
    \ 'f:function:0:1',
    \ 't:type:0:1',
    \ 'i:interface:0:1',
    \ 'c:class:0:1',
    \ 'm:member:0:1',
    \ 'v:variable:0:1',
  \ ],
  \ 'sort' : 0
\ }

" ------------------------------------------------------------------------------
" Test Config
" ------------------------------------------------------------------------------
let test#strategy = 'asnycrun'
let g:test#runner_commands=['Jasmine']

" ------------------------------------------------------------------------------
" Vimwiki Config
" ------------------------------------------------------------------------------
let g:vimwiki_folding = 'expr'
let g:vimwiki_global_ext = 0
let g:vimwiki_table_mappings = 0
let g:vimwiki_list = [
    \ {
    \ 'path': '~/work/vim/vimwiki',
    \ 'syntax': 'markdown', 'ext': '.md',
    \ 'nested_syntaxes': {
        \ 'cs': 'cs',
        \ 'sql': 'sql',
        \ }
    \ },
    \
    \ {
    \ 'path': '~/Dropbox/vimwiki/',
    \ 'syntax': 'markdown', 'ext': '.md',
    \ }]

" }}}

" Leader Mappings {{{

" System Clipboard
vnoremap <leader>c "+y|                                  " Copy
nnoremap <leader>v "+p|                                  " Paste (normal)
vnoremap <leader>v "+p|                                  " Paste (visual)

" Buffer
nnoremap <leader><Tab> :b#<CR>|                          " Switch to last buffer
nnoremap <leader>bo :BufOnly<CR>|                        " Keep only this buffer
nnoremap <leader>bO :DeleteInactiveBufs<CR>|             " Delete buffers

" Formatting
nnoremap <leader>fw :call ToggleWrap()<CR>|              " Toggle wrap

" Linting (Errors)
nnoremap <leader>eo :lopen<CR>|                          " Open
nnoremap <leader>ec :lclose<CR>|                         " Close
nnoremap <leader>ee :ll<CR>|                             " Current
nnoremap <leader>ej :lnext<CR>|                          " Next
nnoremap <leader>ek :lprevious<CR>|                      " Previous

" Meta
nnoremap <leader>_d :e $MYVIMRC<CR>|                     " Edit .vimrc
nnoremap <leader>_r :source $MYVIMRC<CR>|                " Reload .vimrc
nnoremap <leader>_f :Explore $HOME/.vim/ftplugin<CR>|    " Open ftplugins
nnoremap <leader>_b :call ToggleBackground()<CR>|        " Toggle background

" Make
nnoremap <leader>mm :Make<CR>|                           " Make

" Undo
nnoremap <leader>uu :GundoToggle<CR>|                    " View undo tree

" Project
nnoremap <leader>pt :Explore .<CR>|                      " Open cwd
nnoremap <leader>pf :Explore<CR>|                        " Open buffer directory
nnoremap <leader>pc :pclose<CR>|                         " Close preview-window
nnoremap <leader>pT :call RunCtags()<CR>|                " Async ctags generate

" Grep
nnoremap <leader>gg :Ag |                                 " Grep
vnoremap <leader>gg "zy:Ag <C-R>z |                       " Visual grep
nnoremap <leader>gG :AgG |                                " Grep
vnoremap <leader>gG "zy:AgG <C-R>z |                      " Visual grep

" Git
nnoremap <leader>gs :GFiles?<CR>|                         " Git status

" Quickfix
nnoremap <leader>qo :botright copen<CR>|                 " Open (full width)
nnoremap <leader>qc :cclose<CR>|                         " Close
nnoremap <leader>qj :cnext<CR>|                          " Next
nnoremap <leader>qk :cprevious<CR>|                      " Previous

" Substitute & Global
nnoremap <leader>ss :%s/|                                " Substitute
vnoremap <leader>ss "zy:%s/<C-R>z/|                      " Substitute
nnoremap <leader>sg :%g/|                                " Global
vnoremap <leader>sg "zy:%g/<C-R>z/|                      " Global
nnoremap <leader>sG :%g!/|                               " Inverse Global
vnoremap <leader>sG "zy:%g!/<C-R>z/|                     " Inverse Global

" Tags
nnoremap <leader>tt :Tags<CR>|                           " Fuzzy-search tags
nnoremap <leader>tb :TagbarOpenAutoClose<CR>|            " Open Tagbar

" }}}

" Work {{{

if isdirectory($HOME . '/work/vim')
    source ~/work/vim/*.vim
endif

" }}}
