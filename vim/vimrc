" Set local foldmarker
" vim:fdm=marker

" Editor {{{

" Use Vim settings, rather then Vi settings
" This must be first, because it changes other options as a side effect
set nocompatible

" ------------------------------------------------------------------------------
" General Config
" ------------------------------------------------------------------------------
set number                     " Line numbers
set relativenumber             " Relative numbering
set backspace=indent,eol,start " Allow backspace in insert mode
set history=1000               " Store lots of :cmdline history
set showcmd                    " Show incomplete cmds down the bottom
set showmode                   " Show current mode down the bottom
set visualbell                 " No sounds
set autoread                   " Reload files changed outside vim
set hidden                     " Allow buffers to exist in background
set noswapfile                 " No swap file
set nobackup                   " No backup
set nowb                       " No write-backup
set laststatus=2               " Always show statusline
set t_Co=16                    " Set terminal color to 16
set colorcolumn=80             " Highlight column 80
set textwidth=80               " Textwidth at column 80
set formatoptions-=t           " Don't auto-wrap at textwidth
set cursorline                 " Highlight current line
set completeopt=menuone        " Complete option (don't show preview)
set wildmenu                   " Tab through commands
set cryptmethod=blowfish2      " Better encryption
set mouse=a                    " Mouse support
set diffopt+=vertical          " Prefer vertical diff
syntax on                      " Turn on syntax
syntax enable                  " Enable syntax
filetype plugin indent on      " Enable filetypes

" ------------------------------------------------------------------------------
" Default for tabs
" ------------------------------------------------------------------------------
set tabstop=4
set softtabstop=4
set shiftwidth=4

" ------------------------------------------------------------------------------
" Indentation and Linebreaks
" ------------------------------------------------------------------------------
set autoindent
set smartindent
set breakindent
set smarttab
set expandtab
set nowrap
set linebreak
set listchars=eol:¬,tab:→-,trail:·,extends:>,precedes:<

" ------------------------------------------------------------------------------
" Search Config
" ------------------------------------------------------------------------------
set incsearch
set ignorecase
set smartcase

" ------------------------------------------------------------------------------
"  Set ESC and Leader
" ------------------------------------------------------------------------------
imap jj <Esc>
let mapleader=' '

" ------------------------------------------------------------------------------
" Normal Mappings
" ------------------------------------------------------------------------------
nnoremap gb :ls<CR>:buffer<Space>

" ------------------------------------------------------------------------------
" Visual Mappings
" ------------------------------------------------------------------------------
vnoremap * "zy/<C-R>z<CR>| " Search for selection using z-register
vnoremap @@ :norm! @@<CR>| " Replay last macro on selection

" ------------------------------------------------------------------------------
" Command Mappings
" ------------------------------------------------------------------------------
cmap w!! w !sudo tee > /dev/null %| " Save as sudo

" ------------------------------------------------------------------------------
" Fold Config
" ------------------------------------------------------------------------------
set foldmethod=indent
" Set foldlevel to 1 > max fold
autocmd BufWinEnter *
    \ let &foldlevel = max(map(range(1, line('$')), 'foldlevel(v:val)'))

" ------------------------------------------------------------------------------
"  Configure Directories
" ------------------------------------------------------------------------------
set runtimepath=$HOME/.vim,$VIMRUNTIME

" ------------------------------------------------------------------------------
" Persistent Undo -- Keep undo history across sessions by storing in file
" ------------------------------------------------------------------------------
if has('persistent_undo')
    silent !mkdir ~/.vim/backups > /dev/null 2>&1
    set undodir=~/.vim/backups
    set undofile
endif

" }}}

" Functions {{{

" ------------------------------------------------------------------------------
" Escape XML
" ------------------------------------------------------------------------------
function! EscapeXml(unescape)
    if a:unescape == 0
        silent! s/&/\&amp;/g
        silent! s/"/\&quot;/g
        silent! s/'/\&apos;/g
        silent! s/'/\&#39;/g
        silent! s/’/\&#8217;/g
        silent! s/>/\&gt;/g
        silent! s/</\&lt;/g
    else
        silent! s/&quot;/"/g
        silent! s/&apos;/'/g
        silent! s/&#39;/'/g
        silent! s/&#8217;/’/g
        silent! s/&gt;/>/g
        silent! s/&lt;/</g
        silent! s/&amp;/\&/g
    endif
endfunction

" ------------------------------------------------------------------------------
" Toggle background
" ------------------------------------------------------------------------------
function! ToggleBackground()
    if &background ==# 'dark'
        set background=light
    else
        set background=dark
    endif
endfunction

" ------------------------------------------------------------------------------
" Toggle wrap
" ------------------------------------------------------------------------------
function! ToggleWrap()
  if &wrap ==# 'nowrap'
    setlocal wrap
  else
    setlocal nowrap
  endif
endfunction

" ------------------------------------------------------------------------------
" Read Command
" ------------------------------------------------------------------------------
function! ReadCommand(msgcmd)
    " Capture message into variable
    redir => message
    silent execute a:msgcmd
    redir END

    " Paste to buffer
    silent put=message
endfunction
command! -nargs=+ -complete=command ReadCommand call ReadCommand(<q-args>)

" ------------------------------------------------------------------------------
" InsertDate Command
" ------------------------------------------------------------------------------
function! InsertDate()
    execute "put =strftime('%Y-%m-%d')"
    execute 'norm! kJ'
endfunction
command! -nargs=0 -complete=command InsertDate call InsertDate()

" ------------------------------------------------------------------------------
" RunCtags Command
" ------------------------------------------------------------------------------
function! RunCtags()
    execute 'AsyncRun ctags -R'
    execute 'botright copen'
endfunction

" ------------------------------------------------------------------------------
" Ag Command
" ------------------------------------------------------------------------------
function! Ag(text, ...)
    let a:filetype = get(a:, 1, '')
    execute 'AsyncRunWithQf ag --vimgrep ' . a:filetype . ' "' . a:text . '"'
endfunction
command! -nargs=* -complete=command Ag call Ag(<f-args>)

" ------------------------------------------------------------------------------
" Scratch Command
" ------------------------------------------------------------------------------
function! Scratch()
    execute 'top 15sp'
    execute 'enew'
    execute 'setlocal buftype=nofile'
    execute 'setlocal bufhidden=hide'
    execute 'setlocal noswapfile'
    execute 'file [Scratch]'
endfunction
command! -nargs=0 -complete=command Scratch call Scratch()

" ------------------------------------------------------------------------------
" DeleteInactiveBufs Command
" source: https://stackoverflow.com/a/7321131/3661319
" ------------------------------------------------------------------------------
function! DeleteInactiveBufs()
    " From tabpagebuflist() help, get a list of all buffers in all tabs
    let tablist = []
    for i in range(tabpagenr('$'))
        call extend(tablist, tabpagebuflist(i + 1))
    endfor

    " Below originally inspired by Hara Krishna Dara and Keith Roberts
    " http://tech.groups.yahoo.com/group/vim/message/56425
    let nWipeouts = 0
    for i in range(1, bufnr('$'))
        if bufexists(i) && !getbufvar(i,"&mod") && index(tablist, i) == -1
        " bufno exists AND isn't modified AND isn't in the list of buffers open
        " in windows and tabs
            silent exec 'bwipeout' i
            let nWipeouts = nWipeouts + 1
        endif
    endfor
    echomsg nWipeouts . ' buffer(s) wiped out'
endfunction
command! -nargs=0 -complete=command DeleteInactiveBufs call DeleteInactiveBufs()

" ------------------------------------------------------------------------------
" AsyncRunWithQf Command
" ------------------------------------------------------------------------------
function! AsyncRunWithQf(cmd)
    execute 'AsyncRun! ' . a:cmd
    execute 'botright copen'
endfunction
command! -nargs=1 -complete=command AsyncRunWithQf call AsyncRunWithQf(<f-args>)

" }}}

" {{{ Misc Config

" ------------------------------------------------------------------------------
" ConEmu Config
" ------------------------------------------------------------------------------
if !empty($CONEMUBUILD)
    " Visual
    set term=xterm
    set encoding=utf-8
    set termencoding=&encoding
    set t_Co=256
    let &t_AB="\e[48;5;%dm"
    let &t_AF="\e[38;5;%dm"

    inoremap <Esc>[62~ <C-X><C-E>
    inoremap <Esc>[63~ <C-X><C-Y>
    nnoremap <Esc>[62~ <C-E>
    nnoremap <Esc>[63~ <C-Y>

    " Backspace
    inoremap <Char-0x07F> <BS>
    nnoremap <Char-0x07F> <BS>
endif

" }}}

" Plugins {{{

" ------------------------------------------------------------------------------
" Plug Config
" ------------------------------------------------------------------------------
" Automatically download Plug if vim is started without it
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')

" Editor features
Plug 'jiangmiao/auto-pairs'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'michaeljsmith/vim-indent-object'
Plug 'sjl/gundo.vim'
Plug 'sk1418/QFGrep'
Plug 'jpalardy/vim-slime'
Plug 'christoomey/vim-tmux-navigator'

" File browsing
Plug 'ctrlpvim/ctrlp.vim'
Plug 'tpope/vim-vinegar'

" Terminal fuzzy-searching (no Windows support yet)
if (!has('win32'))
    Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
endif

" Auto-Complete
Plug 'lifepillar/vim-mucomplete'
Plug 'garbas/vim-snipmate'
    \ | Plug 'marcweber/vim-addon-mw-utils'
    \ | Plug 'tomtom/tlib_vim'

" Formatting
Plug 'godlygeek/tabular'
Plug 'ntpeters/vim-better-whitespace'

" Git
Plug 'tpope/vim-fugitive'

" Languages {{{

" Clojure
Plug 'tpope/vim-fireplace', { 'for': 'clojure' }

" CSharp
Plug 'OrangeT/vim-csharp', { 'for': 'cs' }
Plug 'OmniSharp/omnisharp-vim', { 'for': 'cs' }
Plug 'juliosueiras/cakebuild.vim', { 'for': 'cake' }

" FSharp
Plug 'fsharp/vim-fsharp', { 'for': 'fsharp', 'do': 'make' }

" Go
Plug 'fatih/vim-go', { 'for': 'go' }

" HTML
Plug 'alvan/vim-closetag'

" JavaScript
Plug 'ternjs/tern_for_vim', { 'for': 'javascript', 'do': 'npm install' }

" Markdown
Plug 'plasticboy/vim-markdown', { 'for': ['markdown', 'vimwiki'] }

" Powershell
Plug 'PProvost/vim-ps1', { 'for': 'ps1' }

" Python
Plug 'davidhalter/jedi-vim', { 'for': 'python' }

" Scala
Plug 'derekwyatt/vim-scala', { 'for': 'scala' }

" TypeScript
Plug 'leafgarland/typescript-vim', { 'for': 'typescript' }
Plug 'Quramy/tsuquyomi', { 'for': 'typescript' }

" }}}

" Linting
Plug 'scrooloose/syntastic'

" Tags
Plug 'majutsushi/tagbar', { 'on': 'TagbarOpenAutoClose' }

" Testing
Plug 'janko-m/vim-test'

" Tools
Plug 'junegunn/goyo.vim', { 'on': 'Goyo' }
Plug 'kshenoy/vim-signature'
Plug 'tpope/vim-dispatch'
Plug 'skywind3000/asyncrun.vim'
Plug 'vim-scripts/BufOnly.vim'
Plug 'vimwiki/vimwiki'

" Visuals
Plug 'altercation/vim-colors-solarized'
Plug 'bling/vim-airline'
Plug 'vim-airline/vim-airline-themes'

call plug#end()

" }}}

" Plugin Config {{{

" ------------------------------------------------------------------------------
" Airline Config
" ------------------------------------------------------------------------------
" let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#hunks#enabled = 0
let g:airline_powerline_fonts = 1

" ------------------------------------------------------------------------------
" CloseTag Config
" ------------------------------------------------------------------------------
let g:closetag_filenames = '*.html,*.xml,*.config'

" ------------------------------------------------------------------------------
" Color Scheme
" ------------------------------------------------------------------------------
set background=dark
colorscheme solarized

" ------------------------------------------------------------------------------
" Commentary Config
" ------------------------------------------------------------------------------
autocmd FileType cs setlocal commentstring=//\ %s
autocmd FileType dosbatch setlocal commentstring=::\ %s

" ------------------------------------------------------------------------------
" Completion Config
" ------------------------------------------------------------------------------
autocmd FileType cs setlocal omnifunc=OmniSharp#Complete
autocmd FileType python setlocal omnifunc=jedi#completions
autocmd Filetype fsharp setlocal omnifunc=fsharpbinding#python#Complete
autocmd Filetype typescript setlocal omnifunc=tsuquyomi#complete

" ------------------------------------------------------------------------------
" CtrlP Config
" ------------------------------------------------------------------------------
let g:ctrlp_clear_cache_on_exit = 0
let g:ctrlp_cache_dir = $HOME . '/.cache/ctrlp'
let g:ctrlp_working_path_mode = 'a'

if executable('ag')
    let g:ctrlp_user_command = 'ag -l --nocolor -g "" "%s"'
endif

if has('win32')
    let g:ctrlp_prompt_mappings = {
        \ 'PrtBS()': ['<Char-0x07F>', '<c-h>']
    \ }
endif

" ------------------------------------------------------------------------------
" FSharp Config
" ------------------------------------------------------------------------------
let g:fsharp_xbuild_path = 'msbuild'
let g:fsharp_test_runner = 'nunit-console'
let g:fsharp_only_check_errors_on_write = 1
let g:fsharp_map_keys = 0
let g:fsharp_completion_helptext = 0

" ------------------------------------------------------------------------------
" Goyo Config
" ------------------------------------------------------------------------------
let g:goyo_width=80

" ------------------------------------------------------------------------------
" Jedi Config
" ------------------------------------------------------------------------------
let g:jedi#auto_vim_configuration = 0
let g:jedi#popup_on_dot = 0
let g:jedi#auto_initialization = 0
let g:jedi#show_call_signatures = 0

" ------------------------------------------------------------------------------
" MUcomplete Config
" ------------------------------------------------------------------------------
let g:mucomplete#user_mappings = { 'snip' : "\<plug>snipMateShow" }
let g:mucomplete#chains = {
  \ 'default' : ['snip', 'path', 'omni', 'keyn', 'dict'],
  \ }
" Remap so can be used <C-J>
imap <_> <plug>(MUcompleteCycFwd)
imap <expr> <C-J> (pumvisible() ? "\<c-y>" : "")
\               . "\<plug>snipMateNextOrTrigger"

" ------------------------------------------------------------------------------
" Netrw Config
" ------------------------------------------------------------------------------
let g:netrw_localrmdir='rm -r' " Ability to remove non-empty directories

" ------------------------------------------------------------------------------
" Salve Config
" ------------------------------------------------------------------------------
let g:salve_auto_start_repl = 1

" ------------------------------------------------------------------------------
" Signature Config
" ------------------------------------------------------------------------------
let g:SignatureMarkTextHLDynamic = 1
let g:SignatureMarkerTextHLDynamic = 1

" ------------------------------------------------------------------------------
" Slime Config
" ------------------------------------------------------------------------------
let g:slime_target = 'tmux'

" ------------------------------------------------------------------------------
" Snipmate Config
" ------------------------------------------------------------------------------
" Prevents errors after sourcing .vimrc
let g:snipMate = get(g:, 'snipMate', {})
let g:snipMate['no_match_completion_feedkeys_chars'] = ''

" ------------------------------------------------------------------------------
" Syntastic Config
" ------------------------------------------------------------------------------
let g:syntastic_always_populate_loc_list = 1

" ------------------------------------------------------------------------------
" Test Config
" ------------------------------------------------------------------------------
let test#strategy = 'asnycrun'
let g:test#runner_commands=['Jasmine']

" ------------------------------------------------------------------------------
" Vimwiki Config
" ------------------------------------------------------------------------------
let g:vimwiki_folding = 'expr'
let g:vimwiki_global_ext = 0
let g:vimwiki_table_mappings = 0
let g:vimwiki_list = [
    \ {
    \ 'path': '~/work/vim/vimwiki',
    \ 'syntax': 'markdown', 'ext': '.md',
    \ 'nested_syntaxes': {
        \ 'cs': 'cs',
        \ 'sql': 'sql',
        \ }
    \ },
    \
    \ {
    \ 'path': '~/Dropbox/vimwiki/',
    \ 'syntax': 'markdown', 'ext': '.md',
    \ }]

" }}}

" Leader Mappings {{{

" System Clipboard
vnoremap <leader>c "+y|                                  " Copy
nnoremap <leader>v "+p|                                  " Paste (normal)
vnoremap <leader>v "+p|                                  " Paste (visual)

" Buffer
nnoremap <leader><Tab> :b#<CR>|                          " Switch to last buffer
nnoremap <leader>bo :BufOnly<CR>|                        " Keep only this buffer
nnoremap <leader>bO :DeleteInactiveBufs<CR>|             " Delete buffers

" Formatting
nnoremap <leader>fa :setlocal formatoptions+=a<CR>|      " Activate auto-format
nnoremap <leader>fA :setlocal formatoptions-=a<CR>|      " Disable auto-format
nnoremap <leader>fw :call ToggleWrap()<CR>|              " Toggle wrap

" Linting (Errors)
nnoremap <leader>ee :SyntasticCheck<CR>|                 " Error check
nnoremap <leader>et :SyntasticToggleMode<CR>|            " Toggle syntastic
nnoremap <leader>eo :lopen<CR>|                          " Open
nnoremap <leader>ec :lclose<CR>|                         " Close
nnoremap <leader>ej :lnext<CR>|                          " Next
nnoremap <leader>ek :lprevious<CR>|                      " Previous

" Meta
nnoremap <leader>_d :e $MYVIMRC<CR>|                     " Edit .vimrc
nnoremap <leader>_r :source $MYVIMRC<CR>|                " Reload .vimrc
nnoremap <leader>_f :Explore $HOME/.vim/ftplugin<CR>|    " Open ftplugins
nnoremap <leader>_b :call ToggleBackground()<CR>|        " Toggle background

" Make
nnoremap <leader>mm :Make<CR>|                           " Make

" Undo
nnoremap <leader>uu :GundoToggle<CR>|                    " View undo tree

" Project
nnoremap <leader>pt :Explore .<CR>|                      " Open cwd
nnoremap <leader>pf :Explore<CR>|                        " Open buffer directory
nnoremap <leader>pc :pclose<CR>|                         " Close preview-window
nnoremap <leader>pT :call RunCtags()<CR>|                " Async ctags generate

" Grep
nnoremap <leader>gg :Ag |                                " Grep
vnoremap <leader>gg "zy:Ag <C-R>z |                      " Visual grep

" Quickfix
nnoremap <leader>qo :botright copen<CR>|                 " Open (full width)
nnoremap <leader>qc :cclose<CR>|                         " Close
nnoremap <leader>qj :cnext<CR>|                          " Next
nnoremap <leader>qk :cprevious<CR>|                      " Previous

" Substitute & Global
nnoremap <leader>ss :%s/|                                " Substitute
nnoremap <leader>sg :%g/|                                " Global
nnoremap <leader>sG :%g!/|                               " Inverse Global

" Spelling
nnoremap <leader>Ss :setlocal spell spelllang=en_us<CR>| " Activate spelling
nnoremap <leader>SS :setlocal nospell<CR>|               " Disable spelling
nnoremap <leader>Sf z=|                                  " Fix word

" Tags
nnoremap <leader>tt :CtrlPTag<CR>|                       " Search tags
nnoremap <leader>tb :CtrlPBufTag<CR>|                    " Search tags in buffer
nnoremap <leader>tB :TagbarOpenAutoClose<CR>|            " Open Tagbar

" Windows
nnoremap <leader>wv :vsplit<CR>|                         " Split vertically
nnoremap <leader>wh :split<CR>|                          " Split horizontally
nnoremap <leader>w= <C-w>=|                              " Reset size
nnoremap <leader>h <C-w>h|                               " Navigate left
nnoremap <leader>j <C-w>j|                               " Navigate down
nnoremap <leader>k <C-w>k|                               " Navigate up
nnoremap <leader>l <C-w>l|                               " Navigate right

" }}}

" Work {{{

if isdirectory($HOME . '/work/vim')
    source ~/work/vim/*.vim
endif

" }}}
